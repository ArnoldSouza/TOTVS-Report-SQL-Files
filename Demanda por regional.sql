/* DECLARAÇÃO INICIAL DE CONFIGURAÇÃO */
SET NOCOUNT ON

--DEFINE AS DATAS A PARTIR DO MÊS PASSADO DE ACORDO COM A DATA DE HOJE 
DECLARE @ANOMES varchar(6) = CONCAT(DATEPART(yyyy, DATEADD(m, -1, getdate())), RIGHT('00'+CAST(DATEPART(m, DATEADD(m, -1, getdate())) AS VARCHAR(2)),2));
DECLARE @cols AS NVARCHAR(MAX), @query  AS NVARCHAR(MAX);

/* CHECA SE A TABELA TEMPORÁRIA #movimentos JÁ EXISTE E A DELETA */
IF OBJECT_ID('tempdb..#SAIDAS') IS NOT NULL DROP TABLE #SAIDAS
/* -------------------------------------------------------------------------- */
/* --------------- INICIA CALCULO DE MOVIMENTOS ----------------------------- */
/* -------------------------------------------------------------------------- */
-- ALOCA O RESULTADO DE TODA A CONSULTA PARA A TABELA TEMPORARIA @MOVIMENTOS
SELECT TEMPORARIA.*
INTO #SAIDAS
FROM
	(
		SELECT
			RTRIM(SD3010.D3_COD) AS 'COD', 
			RTRIM(SB1010.B1_DESC) AS 'DESCRICAO',
			SD3010.D3_QUANT AS 'QTDE',  
			LEFT(SD3010.D3_EMISSAO,6) AS 'ANOMES'
		FROM PROTHEUS.dbo.SD3010 SD3010 
			LEFT JOIN PROTHEUS.dbo.SB1010 SB1010 ON SD3010.D3_COD=SB1010.B1_COD AND SB1010.D_E_L_E_T_ <> '*'	
		WHERE
			(SD3010.D_E_L_E_T_<>'*') AND
			(LEFT(SD3010.D3_CF,2) = 'RE') AND
			(SD3010.D3_ESTORNO='') AND
			((SD3010.D3_DOC NOT LIKE '%INVENT%') AND (SD3010.D3_TM<>'503')) AND --DESCONSIDERA REGULARIZAÇÕES DE INVENTÁRIO
			(SD3010.D3_TM<>'999') AND --DESCONSIDERA TRANSFERÊNCIA
			(SD3010.D3_TM<>'504') AND --DESCONSIDERA OUTROS MOVIMENTOS
			((SD3010.D3_FILIAL='07') AND (SD3010.D3_LOCAL IN ('45','47','42','40','49'))) AND
			(SD3010.D3_EMISSAO>='20180101') AND (LEFT(SD3010.D3_EMISSAO,6)<=@ANOMES) AND
			(SD3010.D3_CLVL <> '001') --DESCONSIDERA MOBILIZAÇÃO

		Union ALL

		SELECT
			RTRIM(SD3010.D3_COD) AS 'COD', 
			RTRIM(SB1010.B1_DESC) AS 'DESCRICAO',
			SD3010.D3_QUANT*-1 AS 'QTDE',  
			LEFT(SD3010.D3_EMISSAO,6) AS 'ANOMES'
		FROM PROTHEUS.dbo.SD3010 SD3010 
			LEFT JOIN PROTHEUS.dbo.SB1010 SB1010 ON SD3010.D3_COD=SB1010.B1_COD AND SB1010.D_E_L_E_T_ <> '*'
		WHERE
			(SD3010.D_E_L_E_T_<>'*') AND
			(LEFT(SD3010.D3_CF,2) = 'DE') AND
			(SD3010.D3_ESTORNO='') AND
			((SD3010.D3_DOC NOT LIKE '%INVENT%') AND (SD3010.D3_TM<>'003')) AND --DESCONSIDERA REGULARIZAÇÕES DE INVENTÁRIO
			(SD3010.D3_TM<>'499') AND --DESCONSIDERA TRANSFERÊNCIA
			(SD3010.D3_TM<>'004') AND --DESCONSIDERA OUTROS MOVIMENTOS
			((SD3010.D3_FILIAL='07') AND (SD3010.D3_LOCAL IN ('45','47','42','40','49'))) AND
			(SD3010.D3_EMISSAO>='20180101') AND (LEFT(SD3010.D3_EMISSAO,6)<=@ANOMES) AND
			(SD3010.D3_CLVL <> '001') -- DESCONSIDERA MOBILIZAÇÃO
	) AS TEMPORARIA

SELECT @cols = STUFF(
						(
							SELECT ',' + QUOTENAME(ANOMES) 
							FROM #SAIDAS
							GROUP BY [ANOMES]
							ORDER BY MIN(ANOMES) FOR XML PATH(''), TYPE
						).value('.', 'NVARCHAR(MAX)'),
						1,
						1,
						''
					)				

--SELECT @cols = STUFF(
--						(
--							SELECT DISTINCT ',' + QUOTENAME(ANOMES) 
--							FROM #SAIDAS
--							FOR XML PATH(''), TYPE
--						).value('.', 'NVARCHAR(MAX)'),
--						1,
--						1,
--						''
--					)				
--SELECT @cols

set @query = 'SELECT *
			  FROM #SAIDAS
			  PIVOT 
				(
					SUM(QTDE)
					FOR ANOMES IN (' + @cols + ')
				) AS P'

execute(@query)

--/* FINALIZA ELIMINANDO A TABELA TEMPORÁRIA */
DROP TABLE #SAIDAS